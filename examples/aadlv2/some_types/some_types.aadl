package Some_types
public
  with Software;
  with Deployment;
  with Buses::Ethernet;
  with Native_Sockets;

  ---------------
  -- Processor --
  ---------------

  processor the_processor
  properties
    Deployment::Execution_Platform => Native;
    Priority_Range => 0 .. 255;
  end the_processor;

  ---------------
  -- Processes --
  ---------------

  process A
  features
    Out_Boolean : out event data port Software::Boolean_Type;
    Out_Integer : out event data port Software::Integer_Type;
    Out_Array   : out event data port Software::Array_Type.I;
    out_String  : out event data port Software::String_Type;
  end A;

  process implementation A.Impl
  subcomponents
    Emitter_Thread : thread Software::Emitter.Impl;
  connections
    port Emitter_Thread.Boolean_Source -> Out_Boolean;
    port Emitter_Thread.Integer_Source -> Out_Integer;
    port Emitter_Thread.Array_Source   -> Out_Array;
    port Emitter_Thread.String_Source  -> Out_String;
  end A.Impl;

  process B
  features
    In_Boolean  : in event data port Software::Boolean_Type;
    In_Integer  : in event data port Software::Integer_Type;
    In_Array    : in event data port Software::Array_Type.I;
    In_String   : in event data port Software::String_Type;
  end B;

  process implementation B.Impl
  subcomponents
    Boolean_Rcv_Thread : thread Software::Boolean_Receiver.Impl;
    Integer_Rcv_Thread : thread Software::Integer_Receiver.Impl;
    Array_Rcv_Thread   : thread Software::Array_Receiver.Impl;
    String_Rcv_Thread  : thread Software::String_Receiver.Impl;
  connections
    port In_Boolean -> Boolean_Rcv_Thread.Data_Sink;
    port In_Integer -> Integer_Rcv_Thread.Data_Sink;
    port In_Array  -> Array_Rcv_Thread.Data_Sink;
    port In_String -> String_Rcv_Thread.Data_Sink;
  end B.Impl;

  ------------
  -- System --
  ------------

  system Some_Types
  end Some_Types;

  system implementation Some_Types.Native
  subcomponents
    the_bus : bus Buses::Ethernet::Ethernet.impl;

    CPU_A : processor the_processor;
    Node_A : process A.Impl;
    Eth1   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };

    CPU_B : processor the_processor;
    Node_B : process B.Impl;
    Eth2   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };



  connections
    bus access the_bus -> Eth1.Eth;
    bus access the_bus -> Eth2.Eth;

    port Node_A.Out_Boolean -> Node_B.In_Boolean
    {Actual_Connection_Binding => (reference (the_bus));};
    port Node_A.Out_Integer -> Node_B.In_Integer
    {Actual_Connection_Binding => (reference (the_bus));};
    port Node_A.Out_Array -> Node_B.In_Array
    {Actual_Connection_Binding => (reference (the_bus));};
    port Node_A.Out_String -> Node_B.In_String
    {Actual_Connection_Binding => (reference (the_bus));};
  properties
    actual_processor_binding => (reference (CPU_A)) applies to Node_A;
    actual_processor_binding => (reference (CPU_A)) applies to Eth1;

    actual_processor_binding => (reference (CPU_B)) applies to Node_B;
    actual_processor_binding => (reference (CPU_B)) applies to Eth2;
  end Some_Types.Native;

end Some_Types;