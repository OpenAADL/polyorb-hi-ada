--  $Id: producer_consumer.aadl 6713 2009-06-20 20:58:28Z lanarre $

package Producer::Consumer
public
  with Deployment, Software;
  with Buses::Ethernet;
  with Native_Sockets;

  ---------------
  -- Processor --
  ---------------

  processor the_processor
  properties
    Deployment::Execution_Platform => Native;
    Priority_Range => 0 .. 255;
  end the_processor;

  ---------------
  -- Processes --
  ---------------

  process A
  features
    Alpha : out data port Software::Alpha_Type;
    Beta  : in data port Software::Alpha_Type;
  end A;

  process implementation A.Impl
  subcomponents
    Producer        : thread Software::P.Impl;
    Result_Consumer : thread Software::Q.Impl;
  connections
    port Producer.Data_Source -> Alpha;
    port Beta -> Result_Consumer.Data_Sink;
  end A.Impl;

  process B
  features
    Alpha : out data port Software::Alpha_Type;
    Beta  : in data port Software::Alpha_Type;
  end B;

  process implementation B.Impl
  subcomponents
    Consumer        : thread Software::Q.Impl;
    Result_Producer : thread Software::P.Impl;
  connections
    port Beta -> Consumer.Data_Sink;
    port Result_Producer.Data_Source -> Alpha;
  end B.Impl;

  ------------
  -- System --
  ------------

  system PC_Simple
  end PC_Simple;

  system implementation PC_Simple.Native
  subcomponents
    -- HW Platform
    the_bus: bus Buses::Ethernet::Ethernet.impl;

    CPU_A : processor the_processor;
    pr_A : process A.Impl;
    Eth1   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };

    CPU_B : processor the_processor;
    pr_B : process B.Impl;
    Eth2   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1234"; };

  connections
    bus access the_bus -> Eth1.Eth;
    bus access the_bus -> Eth2.Eth;

    port pr_A.Alpha -> pr_B.Beta
    {Actual_Connection_Binding => (reference (the_bus));};
    port pr_B.Alpha -> pr_A.Beta
    {Actual_Connection_Binding => (reference (the_bus));};

  properties
    actual_processor_binding => reference (CPU_A) applies to pr_A;
    actual_processor_binding => reference (CPU_A) applies to Eth1;

    actual_processor_binding => reference (CPU_B) applies to pr_B;
    actual_processor_binding => reference (CPU_B) applies to Eth2;
  end PC_Simple.Native;

end Producer::Consumer;
