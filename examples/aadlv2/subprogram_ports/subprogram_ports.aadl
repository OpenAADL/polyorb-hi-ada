package SubprogramPorts
public
  with Deployment, Software;
  with Buses::Ethernet;
  with Native_Sockets;

  ---------------
  -- Processes --
  ---------------

  process Driver
  features
    Event_Source : out event      port;
    Data_Source  : out event data port Software::Integer_Type;
  end Driver;

  process implementation Driver.Impl
  subcomponents
    Sender : thread Software::Sender_Thread.Impl;
  connections
    port Sender.Event_Source -> Event_Source;
    port Sender.Data_Source  -> Data_Source;
  end Driver.Impl;

  process Receiver_1
  features
    Data_Sink : in event data port Software::Integer_Type;
  end Receiver_1;

  process implementation Receiver_1.Impl
  subcomponents
    Receiver : thread Software::Receiver_Thread.Impl;
  connections
    port Data_Sink -> Receiver.Data_Sink;
  end Receiver_1.Impl;

  process Receiver_2
  features
    Event_Sink : in event port;
  end Receiver_2;

  process implementation Receiver_2.Impl
  subcomponents
    Transmitter : thread Software::Transmitter_Thread.Impl;
    Receiver    : thread Software::Receiver_Thread.Impl;
  connections
    port Event_Sink              -> Transmitter.Event_Sink;
    port Transmitter.Data_Source -> Receiver.Data_Sink;
  end Receiver_2.Impl;

  ---------------
  -- Processor --
  ---------------

  processor the_processor
  properties
    Deployment::Execution_Platform => Native;
    Priority_Range => 0 .. 255;
  end the_processor;

  ------------
  -- System --
  ------------

  system Subprogram_Ports
  end Subprogram_Ports;

  system implementation Subprogram_Ports.Native
  subcomponents
    the_bus: bus Buses::Ethernet::Ethernet.impl;

    CPU        : processor the_processor;
    Driver     : process Driver.Impl;
    Eth0   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };

    CPU_1        : processor the_processor;
    Receiver_1 : process Receiver_1.Impl {Deployment::port_number => 12002;};
    Eth1   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1234"; };

    CPU_2        : processor the_processor;
    Receiver_2 : process Receiver_2.Impl {Deployment::port_number => 12003;};
    Eth2   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1235"; };

  connections
    bus access the_bus -> Eth0.Eth;
    bus access the_bus -> Eth1.Eth;
    bus access the_bus -> Eth2.Eth;

    port Driver.Data_Source  -> Receiver_1.Data_Sink
    {Actual_Connection_Binding => (reference (the_bus));};
    port Driver.Event_Source -> Receiver_2.Event_Sink
    {Actual_Connection_Binding => (reference (the_bus));};

  properties
    actual_processor_binding => reference (CPU) applies to Driver;
    actual_processor_binding => reference (CPU_1) applies to Receiver_1;
    actual_processor_binding => reference (CPU_2) applies to Receiver_2;
    actual_processor_binding => reference (CPU) applies to Eth0;
    actual_processor_binding => reference (CPU_1) applies to Eth1;
    actual_processor_binding => reference (CPU_2) applies to Eth2;
  end Subprogram_Ports.Native;

end SubprogramPorts;