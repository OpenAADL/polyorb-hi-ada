--  This model completes the delayed connections example by adding
--  deployment information.
--
--  In this deployment, only one node is defined.

package DCLocal
public
  with Deployment, Software;

  ---------------
  -- Processor --
  ---------------

  processor the_processor
  properties
    Deployment::Execution_Platform => Native;
    Priority_Range => 0 .. 255;
  end the_processor;

  processor implementation the_processor.Impl
  end the_processor.Impl;

  ---------------
  -- Processes --
  ---------------

  process A
  end A;

  process implementation A.Impl
  subcomponents
    Sender   : thread Software::S.Impl;
    Receiver : thread Software::R.Impl;
  connections
    C1: port Sender.Data_Source -> Receiver.Data_Sink {Timing => Delayed;};
    --  This is a delayed connection. If this connection was immediate,
    --  the receiver would receive a value of Data_Sink which would be
    --  equal to N_Cycle. Because the connection is immediate, the
    --  receiver sees only the previous value of Data_Sink which is
    --  equal to N_Cycle - 1.

    C2: port Sender.N_Cycle     -> Receiver.N_Cycle;
  end A.Impl;

  ------------
  -- System --
  ------------

  system Delayed_Connections
  end Delayed_Connections;

  system implementation Delayed_Connections.Native
  subcomponents
    Node_A : process A.Impl;

    CPU : processor the_processor.Impl;

  properties
    actual_processor_binding => reference (CPU) applies to Node_A;
  end Delayed_Connections.Native;

end DCLocal;
