package Sunseeker_Dist
public
  with Sunseeker;
  with Deployment;
  with Buses::Ethernet;
  with Native_Sockets;

  ---------------
  -- Processor --
  ---------------

  processor Pentium_Type
  properties
    Deployment::Execution_Platform => native;
  end Pentium_Type;

  processor implementation Pentium_Type.Pentium
  properties
    none;
  end Pentium_Type.Pentium;

  ------------
  -- System --
  ------------

  system Sunseekercontrolsystem_Type
  end Sunseekercontrolsystem_Type;

  system implementation Sunseekercontrolsystem_Type.Native
  subcomponents
    the_bus: bus Buses::Ethernet::Ethernet.impl;

    CPU: processor Pentium_Type.Pentium;
    Sunseekerplant : process Sunseeker::Sunseekerplant_Type.Sunseekerplant;
    Eth1   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };

    CPU2: processor Pentium_Type.Pentium;
    Sunseekercontroller : process Sunseeker::Sunseekercontroller_Type.Sunseekercontroller;
    Eth2   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1234"; };

  connections
    bus access the_bus -> Eth1.Eth;
    bus access the_bus -> Eth2.Eth;

    port Sunseekerplant.Outputfeedback -> Sunseekercontroller.Outputfeedback
    {Actual_Connection_Binding => (reference (the_bus));};
    port Sunseekercontroller.Controllerinput -> Sunseekerplant.Controllerinput
    {Actual_Connection_Binding => (reference (the_bus));};

    --  Bind the processes to the processor
  properties
    Actual_Processor_Binding => (reference (CPU)) applies to Sunseekerplant;
    Actual_Processor_Binding => (reference (CPU2)) applies to Sunseekercontroller;

    Actual_Processor_Binding => (reference (CPU)) applies to Eth1;
    Actual_Processor_Binding => (reference (CPU2)) applies to Eth2;
  end Sunseekercontrolsystem_Type.Native;

end Sunseeker_Dist;