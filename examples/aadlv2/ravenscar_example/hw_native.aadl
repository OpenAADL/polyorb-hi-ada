--  $Id: hw_native.aadl 6752 2009-07-01 16:36:45Z lasnier $

package HW_Native
public
  with Deployment;
  with RavenscarExample;
  with Buses::Ethernet;
  with Native_Sockets;

  processor The_Processor
  properties
    Deployment::Execution_Platform => Native;
    Scheduling_Protocol => POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL;
    Priority_Range                           => 0 .. 255;
  end The_Processor;

  system Case_Study
  end Case_Study;

  system implementation Case_Study.Native
  subcomponents
    CPU_1 : processor The_Processor;
    WoM : process RavenscarExample::Workload_Manager.Impl
    {Deployment::Port_Number => 12001;};
    Eth1   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };

    CPU_2 : processor The_Processor;
    InS : process RavenscarExample::Interrupt_Simulator.Impl;
    Eth2   : device Native_Sockets::Native_Sockets.POHI_Ada
    { Deployment::Location    => "ip 127.0.0.1 1233"; };
    B : bus Buses::Ethernet::Ethernet.impl;
  connections
    bus access B -> Eth1.Eth;
    bus access B -> Eth2.Eth;

    port InS.External_Interrupt -> WoM.External_Interrupt_Depository
    {Actual_Connection_Binding => reference (B);};

  properties
    Actual_Processor_Binding => reference (CPU_1) applies to WoM;
    Actual_Processor_Binding => reference (CPU_1) applies to Eth1;

    Actual_Processor_Binding => reference (CPU_2) applies to InS;
    Actual_Processor_Binding => reference (CPU_2) applies to Eth2;
  end Case_Study.Native;

end HW_Native;